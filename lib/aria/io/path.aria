# SPDX-License-Identifier: Apache-2.0
flag: uses_dylib("aria_path");
import aria.date.instant;

struct Path {
    struct Error {
        type func new(msg) {
            return alloc(This) { .msg = msg };
        }

        func prettyprint() {
            return "path error: {0}".format(this.msg);
        }
    }
    
    type func new_with_current_directory() {
        return This._cwd();
    }

    type func new_with_environment_variable(var) {
        return getenv(var).apply(|p| => {
            return This.new(p);
        });
    }

    type func new(s: String) {
        return This._new(s);
    }

    func append(rhs: String|Path) {
        if rhs isa String {
            this._append(rhs);
        } elsif rhs isa Path {
            this._append(rhs.prettyprint());
        }

        return this;
    }

    operator /(rhs: String|Path) {
        return Path.new(this.prettyprint()).append(rhs);
    }

    func parent() {
        return Path.new(this.prettyprint()).pop();
    }

    func created() {
        match this._when_created() {
            case Ok(val) => {
                return Result::Ok(aria.date.instant.Instant.new_with_local_timestamp(val));
            }
            case Err(err) => {
                return Result::Err(err);
            }
        };
    }

    func accessed() {
        return aria.date.instant.Instant.new_with_local_timestamp(this._when_accessed());
    }

    func modified() {
        return aria.date.instant.Instant.new_with_local_timestamp(this._when_modified());
    }

    # we could use the builtin _copy, except for type validation
    func copy_to(other: Path) {
        this._copy(other);
    }
}
